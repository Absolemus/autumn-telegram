#Использовать 1connector

#Область Переменные

&Пластилин Перем Токен;
Перем Письмо; 
Перем URL; 

#КонецОбласти

#Область ОбработчикиСобытий

&Желудь
&Характер("Компанейский")
Процедура ПриСозданииОбъекта()
	Письмо = Новый Структура("Метод, Параметры, json");
	Письмо.Метод = "";
	Письмо.Параметры = Новый Соответствие;
	Письмо.json = "";
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Отправка сообщения
// Возвращаемое значение: 
// 		Ответ - см. КоннекторHTTP.ПодготовленныйОтвет		
Функция Отправить() Экспорт
	УстановитьПараметры();
	Возврат КоннекторHTTP.Get(URL);
КонецФункции

Функция ОтправитьСВложением() Экспорт
	УстановитьПараметры();
	Возврат КоннекторHTTP.Post(URL, Письмо.json);
КонецФункции

// Установка метода
// Параметры:
// 		Метод - строка - метод API
Процедура УстановитьМетод(Знач Метод) Экспорт
	Письмо.Метод = Метод;
КонецПроцедуры

// Установка параметра
// Параметры:
// 		Параметр - строка - название параметра
// 		Значение - любой - значение параметра
Процедура УстановитьПараметр(Знач Параметр, Знач Значение) Экспорт
	Письмо.Параметры.Вставить(Параметр, Значение);
КонецПроцедуры

Процедура Установитьjson(json) Экспорт
	Письмо.json = json;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьПараметры()
	URL = СтрШаблон("https://api.telegram.org/bot%1/%2", Токен, Письмо.Метод);

	Если Письмо.Параметры.Количество() > 0 Тогда
		URL = СтрШаблон("%1?", URL);
		Для Каждого Пара Из Письмо.Параметры Цикл
			URL = СтрШаблон("%1&%2=%3", URL, Пара.Ключ, Пара.Значение);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти